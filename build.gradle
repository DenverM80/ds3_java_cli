/*
 * ******************************************************************************
 *   Copyright 2014 Spectra Logic Corporation. All Rights Reserved.
 *   Licensed under the Apache License, Version 2.0 (the "License"). You may not use
 *   this file except in compliance with the License. A copy of the License is located at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 *   or in the "license" file accompanying this file.
 *   This file is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
 *   CONDITIONS OF ANY KIND, either express or implied. See the License for the
 *   specific language governing permissions and limitations under the License.
 * ****************************************************************************
 */

import java.nio.file.Files
import java.nio.file.Path

plugins {
    id "com.github.hierynomus.license" version "0.11.0"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'application'

version = '1.1.2'
group = 'com.spectralogic'

mainClassName = 'com.spectralogic.ds3cli.Main'

repositories {
  mavenCentral()
  jcenter()
  mavenLocal()
  maven {
    url 'https://spectralogic.github.io/java/repository'
  }
}

dependencies {
  compile fileTree(dir: 'lib', include: '*.jar')
  compile 'commons-cli:commons-cli:1.2'
  compile 'com.google.guava:guava:18.0'
  compile 'joda-time:joda-time:2.7'
  compile 'com.spectralogic.ds3:ds3-sdk:1.1.0-RC3'
  compile (
    [group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.2.3'],
    [group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.2.3'],
    [group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.2.3']
  )
  testCompile "org.mockito:mockito-core:1.+"
  testCompile 'junit:junit:4.11'
}

task genConfigProperties << {
    File configFile = new File(sourceSets.main.output.resourcesDir, "/config.properties")
    Path configPath = sourceSets.main.output.resourcesDir.toPath()
    if (!Files.exists(configPath)) {
        Files.createDirectories(configPath)
    }

    configFile.withWriter{out ->
        out.writeLine("version="+version)
        out.writeLine("build.date="+new Date().toString())
    }
}

jar.dependsOn genConfigProperties

task wrapper(type: Wrapper) {
  gradleVersion = '2.3'
}
